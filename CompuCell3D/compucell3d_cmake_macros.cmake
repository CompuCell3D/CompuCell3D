#helpful macros
MACRO(LIST_CONTAINS var value)
  SET(${var})
  FOREACH (value2 ${ARGN})
    IF (${value} STREQUAL ${value2})
      SET(${var} TRUE)
    ENDIF (${value} STREQUAL ${value2})
  ENDFOREACH (value2)
ENDMACRO(LIST_CONTAINS)

MACRO(PARSE_ARGUMENTS prefix arg_names option_names)
  SET(DEFAULT_ARGS)
  FOREACH(arg_name ${arg_names})
    SET(${prefix}_${arg_name})
  ENDFOREACH(arg_name)
  FOREACH(option ${option_names})
    SET(${prefix}_${option} FALSE)
  ENDFOREACH(option)

  SET(current_arg_name DEFAULT_ARGS)
  SET(current_arg_list)
  FOREACH(arg ${ARGN})
    LIST_CONTAINS(is_arg_name ${arg} ${arg_names})
    IF (is_arg_name)
      SET(${prefix}_${current_arg_name} ${current_arg_list})
      SET(current_arg_name ${arg})
      SET(current_arg_list)
    ELSE (is_arg_name)
      LIST_CONTAINS(is_option ${arg} ${option_names})
      IF (is_option)
	SET(${prefix}_${arg} TRUE)
      ELSE (is_option)
	SET(current_arg_list ${current_arg_list} ${arg})
      ENDIF (is_option)
    ENDIF (is_arg_name)
  ENDFOREACH(arg)
  SET(${prefix}_${current_arg_name} ${current_arg_list})
ENDMACRO(PARSE_ARGUMENTS)


MACRO(CAR var)
  SET(${var} ${ARGV1})
ENDMACRO(CAR)


MACRO(CDR var junk)
  SET(${var} ${ARGN})
ENDMACRO(CDR)

MACRO(EXPORT_COMPONENT library_name component_name)
  install(EXPORT ${library_name}
          NAMESPACE cc3d:: 
          DESTINATION ${COMPUCELL3D_INSTALL_CONFIG_DIR} 
          FILE cc3d-${component_name}-config.cmake 
          EXPORT_LINK_INTERFACE_LIBRARIES 
          COMPONENT ${component_name}
  )
ENDMACRO(EXPORT_COMPONENT)

# notice that variables listed in PARSE_ARGUMENTS function (e.g. "LINK_LIBRARIES;DEPENDS;SUFFIX;COMPILE_FLAGS")
# have to be prepended with "LIBRARY_" before being referenced
MACRO(ADD_STATIC_LIBRARY)
   PARSE_ARGUMENTS(LIBRARY
    "LINK_LIBRARIES;DEPENDS;SUFFIX;EXTRA_COMPILER_FLAGS"
    ""
    ${ARGN}
    )
  CAR(LIBRARY_NAME ${LIBRARY_DEFAULT_ARGS})
  CDR(LIBRARY_SOURCES ${LIBRARY_DEFAULT_ARGS})


  ADD_LIBRARY(${LIBRARY_NAME}Static STATIC ${LIBRARY_SOURCES})
  TARGET_LINK_LIBRARIES(${LIBRARY_NAME}Static ${LIBRARY_LINK_LIBRARIES})

    IF(USE_LIBRARY_VERSIONS)
      SET_TARGET_PROPERTIES(
        ${LIBRARY_NAME}Static PROPERTIES
        ${COMPUCELL3D_LIBRARY_PROPERTIES}
      )
    ENDIF(USE_LIBRARY_VERSIONS)

  SET_TARGET_PROPERTIES(${LIBRARY_NAME}Static  PROPERTIES OUTPUT_NAME CC3D${LIBRARY_NAME}${LIBRARY_SUFFIX} COMPILE_FLAGS "${LIBRARY_EXTRA_COMPILER_FLAGS}")
  install(TARGETS ${LIBRARY_NAME}Static
          EXPORT ${LIBRARY_NAME}-static 
          LIBRARY DESTINATION lib
          ARCHIVE DESTINATION lib
  )
  EXPORT_COMPONENT(${LIBRARY_NAME}-static ${LIBRARY_NAME}-static)
  set(${LIBRARY_NAME}Lib ${LIBRARY_NAME}Static)
  add_library(cc3d::${LIBRARY_NAME} ALIAS ${LIBRARY_NAME}Static)
ENDMACRO(ADD_STATIC_LIBRARY)

# notice that variables listed in PARSE_ARGUMENTS function (e.g. "LINK_LIBRARIES;DEPENDS;SUFFIX;COMPILE_FLAGS") have
# to be prepended with "LIBRARY_" before being referenced
MACRO(ADD_SHARED_LIBRARY)
    PARSE_ARGUMENTS(LIBRARY
        "LINK_LIBRARIES;DEPENDS;SUFFIX;EXTRA_COMPILER_FLAGS"
        ""
        ${ARGN}
    )
    CAR(LIBRARY_NAME ${LIBRARY_DEFAULT_ARGS})
    CDR(LIBRARY_SOURCES ${LIBRARY_DEFAULT_ARGS})

    ADD_LIBRARY(${LIBRARY_NAME}Shared SHARED ${LIBRARY_SOURCES})
    TARGET_LINK_LIBRARIES(${LIBRARY_NAME}Shared ${LIBRARY_LINK_LIBRARIES})

    IF(USE_LIBRARY_VERSIONS)
      SET_TARGET_PROPERTIES(
        ${LIBRARY_NAME}Shared PROPERTIES
        ${COMPUCELL3D_LIBRARY_PROPERTIES}
      )
    ENDIF(USE_LIBRARY_VERSIONS)

    SET_TARGET_PROPERTIES(${LIBRARY_NAME}Shared PROPERTIES OUTPUT_NAME CC3D${LIBRARY_NAME}${LIBRARY_SUFFIX} COMPILE_FLAGS "${LIBRARY_EXTRA_COMPILER_FLAGS}")
    install(TARGETS ${LIBRARY_NAME}Shared 
            EXPORT ${LIBRARY_NAME} 
            RUNTIME DESTINATION ${COMPUCELL3D_RUNTIME_DESTINATION}
            LIBRARY DESTINATION ${COMPUCELL3D_LIBRARY_DESTINATION}
            ARCHIVE DESTINATION ${COMPUCELL3D_ARCHIVE_DESTINATION}
    )
    EXPORT_COMPONENT(${LIBRARY_NAME} ${LIBRARY_NAME})
    set(${LIBRARY_NAME}Lib ${LIBRARY_NAME}Shared)
    add_library(cc3d::${LIBRARY_NAME} ALIAS ${LIBRARY_NAME}Shared)
ENDMACRO(ADD_SHARED_LIBRARY)



MACRO(ADD_HEADERS targetDir)

  file(GLOB header_files "${CMAKE_CURRENT_SOURCE_DIR}/*.h" "${CMAKE_CURRENT_SOURCE_DIR}/*.hpp" "${CMAKE_CURRENT_SOURCE_DIR}/*.hxx")
  INSTALL(FILES ${header_files} DESTINATION ${targetDir})

ENDMACRO(ADD_HEADERS)

MACRO(ADD_COMPUCELL3D_PLUGIN_HEADERS plugin)

  file(GLOB header_files "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
  INSTALL(FILES ${header_files} DESTINATION "${CMAKE_INSTALL_PREFIX}/include/CompuCell3D/CompuCell3D/plugins/${plugin}")


ENDMACRO(ADD_COMPUCELL3D_PLUGIN_HEADERS)

MACRO(ADD_COMPUCELL3D_PLUGIN)
    PARSE_ARGUMENTS(LIBRARY
        "LINK_LIBRARIES;DEPENDS;SUFFIX;EXTRA_COMPILER_FLAGS"
        ""
        ${ARGN}
    )
    CAR(LIBRARY_NAME ${LIBRARY_DEFAULT_ARGS})
    CDR(LIBRARY_SOURCES ${LIBRARY_DEFAULT_ARGS})

    file(GLOB source_files "${CMAKE_CURRENT_SOURCE_DIR}/*.cpp" "${CMAKE_CURRENT_SOURCE_DIR}/*.cxx" "${CMAKE_CURRENT_SOURCE_DIR}/*.c" )

    ADD_LIBRARY(${LIBRARY_NAME}Shared SHARED ${LIBRARY_SOURCES} ${source_files})
    TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME}Shared PUBLIC 
        $<BUILD_INTERFACE:${COMPUCELL3D_SOURCE_DIR}/core>
        $<BUILD_INTERFACE:${COMPUCELL3D_SOURCE_DIR}/core/CompuCell3D/plugins>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/${COMPUCELL3D_INSTALL_INCLUDE_DIR}>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/${COMPUCELL3D_INSTALL_INCLUDE_DIR}/CompuCell3D/plugins>
    )
    TARGET_LINK_LIBRARIES(${LIBRARY_NAME}Shared ${LIBRARY_LINK_LIBRARIES})

    IF(USE_LIBRARY_VERSIONS)
        SET_TARGET_PROPERTIES(
            ${LIBRARY_NAME}Shared PROPERTIES
            ${COMPUCELL3D_LIBRARY_PROPERTIES}
        )
    ENDIF(USE_LIBRARY_VERSIONS)

    SET_TARGET_PROPERTIES(${LIBRARY_NAME}Shared  PROPERTIES OUTPUT_NAME CC3D${LIBRARY_NAME}${LIBRARY_SUFFIX} COMPILE_FLAGS "${LIBRARY_EXTRA_COMPILER_FLAGS}")
    set(includes_dir ${CMAKE_INSTALL_PREFIX}/include/CompuCell3D/CompuCell3D/plugins/${LIBRARY_NAME})
    install(TARGETS ${LIBRARY_NAME}Shared 
            EXPORT ${LIBRARY_NAME} 
            RUNTIME DESTINATION ${COMPUCELL3D_INSTALL_PLUGIN_DIR} 
            LIBRARY DESTINATION ${COMPUCELL3D_INSTALL_PLUGIN_DIR} 
            ARCHIVE DESTINATION ${COMPUCELL3D_INSTALL_PLUGIN_DIR} 
            INCLUDES DESTINATION ${includes_dir}
    )
    EXPORT_COMPONENT(${LIBRARY_NAME} plugin-${LIBRARY_NAME})

    ADD_HEADERS("${includes_dir}")
    set(${LIBRARY_NAME}Lib ${LIBRARY_NAME}Shared)
    add_library(cc3d::${LIBRARY_NAME} ALIAS ${LIBRARY_NAME}Shared)

ENDMACRO(ADD_COMPUCELL3D_PLUGIN)



MACRO(ADD_COMPUCELL3D_STEPPABLE)
    PARSE_ARGUMENTS(LIBRARY
        "LINK_LIBRARIES;DEPENDS;SUFFIX;EXTRA_COMPILER_FLAGS"
        ""
    ${ARGN}
    )
    CAR(LIBRARY_NAME ${LIBRARY_DEFAULT_ARGS})
    CDR(LIBRARY_SOURCES ${LIBRARY_DEFAULT_ARGS})

    file(GLOB source_files "${CMAKE_CURRENT_SOURCE_DIR}/*.cpp" "${CMAKE_CURRENT_SOURCE_DIR}/*.cxx" "${CMAKE_CURRENT_SOURCE_DIR}/*.c" )

    ADD_LIBRARY(${LIBRARY_NAME}Shared SHARED ${LIBRARY_SOURCES} ${source_files})
    TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME}Shared PUBLIC 
        $<BUILD_INTERFACE:${COMPUCELL3D_SOURCE_DIR}/core>
        $<BUILD_INTERFACE:${COMPUCELL3D_SOURCE_DIR}/core/CompuCell3D/steppables>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/${COMPUCELL3D_INSTALL_INCLUDE_DIR}>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_PREFIX}/${COMPUCELL3D_INSTALL_INCLUDE_DIR}/CompuCell3D/steppables>
    )
    TARGET_LINK_LIBRARIES(${LIBRARY_NAME}Shared ${LIBRARY_LINK_LIBRARIES})

    IF(USE_LIBRARY_VERSIONS)
      SET_TARGET_PROPERTIES(
        ${LIBRARY_NAME}Shared PROPERTIES
        ${COMPUCELL3D_LIBRARY_PROPERTIES}
      )
    ENDIF(USE_LIBRARY_VERSIONS)

    SET_TARGET_PROPERTIES(${LIBRARY_NAME}Shared  PROPERTIES OUTPUT_NAME CC3D${LIBRARY_NAME}${LIBRARY_SUFFIX} COMPILE_FLAGS "${LIBRARY_EXTRA_COMPILER_FLAGS}")
    set(includes_dir ${CMAKE_INSTALL_PREFIX}/include/CompuCell3D/CompuCell3D/steppables/${LIBRARY_NAME})
    install(TARGETS ${LIBRARY_NAME}Shared 
            EXPORT ${LIBRARY_NAME} 
            RUNTIME DESTINATION ${COMPUCELL3D_INSTALL_STEPPABLE_DIR} 
            LIBRARY DESTINATION ${COMPUCELL3D_INSTALL_STEPPABLE_DIR} 
            ARCHIVE DESTINATION ${COMPUCELL3D_INSTALL_STEPPABLE_DIR} 
            INCLUDES DESTINATION ${includes_dir}
    )
    EXPORT_COMPONENT(${LIBRARY_NAME} steppable-${LIBRARY_NAME})

    ADD_HEADERS("${includes_dir}")
    set(${LIBRARY_NAME}Lib ${LIBRARY_NAME}Shared)
    add_library(cc3d::${LIBRARY_NAME} ALIAS ${LIBRARY_NAME}Shared)

ENDMACRO(ADD_COMPUCELL3D_STEPPABLE)

